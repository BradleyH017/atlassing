configfile: "config.yaml"
rule all:
    input:
        expand("results/{tissue}/tables/annotation/CellTypist/CellTypist_anno_conf.csv", tissue=config["tissue"])

rule qc_raw:
    input:
        "/lustre/scratch126/humgen/projects/sc-eqtl-ibd/analysis/bradley_analysis/results/tissues_combined/input/adata_raw_input_{tissue}.h5ad"
    output:
        "results/{tissue}/objects/adata_PCAd.h5ad",
        "results/{tissue}/objects/adata_PCAd_batched.h5ad",
        "results/{tissue}/tables/knee.txt",
        "results/{tissue}/tables/log1p_cp10k_sparse.npz",
        "results/{tissue}/tables/genes.txt",
        "results/{tissue}/tables/cells.txt"
    params:
        discard_other_inflams=config["discard_other_inflams"], 
        all_blood_immune=config["all_blood_immune"],
        min_nUMI=config["min_nUMI"],
        use_absolute_nUMI=config["use_absolute_nUMI"],
        use_relative_mad=config["use_relative_mad"],
        lineage_column=config["lineage_column"],
        relative_grouping=config["relative_grouping"],
        relative_nMAD_threshold=config["relative_nMAD_threshold"],
        relative_nUMI_log=config["relative_nUMI_log"],
        min_nGene=config["min_nGene"],
        use_absolute_nGene=config["use_absolute_nGene"],
        relative_nGene_log=config["relative_nGene_log"],
        MT_thresh_gut=config["MT_thresh_gut"],
        MT_thresh_blood=config["MT_thresh_blood"],
        use_absolute_MT=config["use_absolute_MT"],
        absolute_max_MT=config["absolute_max_MT"],
        min_mean_nCount_per_samp_blood=config["min_mean_nCount_per_samp_blood"],
        min_mean_nCount_per_samp_gut=config["min_mean_nCount_per_samp_gut"],
        min_mean_nGene_per_samp_blood=config["min_mean_nGene_per_samp_blood"],
        min_mean_nGene_per_samp_gut=config["min_mean_nGene_per_samp_gut"],
        use_abs_per_samp=config["use_abs_per_samp"],
        filt_blood_keras=config["filt_blood_keras"],
        n_variable_genes=config["n_variable_genes"],
        remove_problem_genes=config["remove_problem_genes"],
        batch_correction=config["batch_correction"],
        benchmark_batch_correction=config["benchmark_batch_correction"]
    resources:
        mem=300000,
        queue='gpu-normal -gpu -',
        mem_mb=300000,
        mem_mib=300000,
        disk_mb=300000,
        tmpdir="tmp"
    conda:
        "scvi-env"
    shell:
        r"""
        python  bin/001_raw_QC_sm.py \
        --input_file {input} \
        --tissue {wildcards.tissue} \
        --discard_other_inflams {params.discard_other_inflams} \
        --all_blood_immune {params.all_blood_immune} \
        --min_nUMI {params.min_nUMI} \
        --use_absolute_nUMI {params.use_absolute_nUMI} \
        --use_relative_mad {params.use_relative_mad} \
        --lineage_column {params.lineage_column} \
        --relative_grouping {params.relative_grouping} --relative_nMAD_threshold {params.relative_nMAD_threshold} \
        --relative_nUMI_log {params.relative_nUMI_log} \
        --min_nGene {params.min_nGene} \
        --use_absolute_nGene {params.use_absolute_nGene} \
        --relative_nGene_log {params.relative_nGene_log} \
        --MT_thresh_gut {params.MT_thresh_gut} \
        --MT_thresh_blood {params.MT_thresh_blood} \
        --use_absolute_MT {params.use_absolute_MT} \
        --absolute_max_MT {params.absolute_max_MT} \
        --min_mean_nCount_per_samp_blood {params.min_mean_nCount_per_samp_blood} \
        --min_mean_nCount_per_samp_gut {params.min_mean_nCount_per_samp_gut} \
        --min_mean_nGene_per_samp_blood {params.min_mean_nGene_per_samp_blood} \
        --min_mean_nGene_per_samp_gut {params.min_mean_nGene_per_samp_gut} \
        --use_abs_per_samp {params.use_abs_per_samp} \
        --filt_blood_keras {params.filt_blood_keras} \
        --n_variable_genes {params.n_variable_genes} \
        --remove_problem_genes {params.remove_problem_genes} \
        --batch_correction {params.batch_correction} \
        --benchmark_batch_correction {params.benchmark_batch_correction}
        """
  
rule get_nn:
    input:
        "results/{tissue}/objects/adata_PCAd.h5ad",
        "results/{tissue}/tables/knee.txt"
    output:
        "results/{tissue}/tables/optimum_nn.txt"
    params:
        use_matrix="X_pca"
    resources:
        mem=300000,
        queue='long',
        mem_mb=300000,
        mem_mib=300000,
        disk_mb=300000,
        tmpdir="tmp"
    conda:
        "bbknn"
    shell:
        r"""
        python bin/002_bbknn.py \
            --tissue {wildcards.tissue} \
            --fpath {input[0]} \
            --knee_file {input[1]} \
            --use_matrix {params.use_matrix}
        """


rule get_umap:
    input:
        "results/{tissue}/objects/adata_PCAd_batched.h5ad",
        "results/{tissue}/tables/knee.txt",
        "results/{tissue}/tables/optimum_nn.txt"
    output:
        "results/{tissue}/objects/adata_PCAd_batched_umap.h5ad"
    params:
        use_matrix="X_pca_harmony,X_pca",
        col_by=config["col_by"]
    resources:
        mem=300000,
        queue='long',
        mem_mb=300000,
        mem_mib=300000,
        disk_mb=300000,
        tmpdir="tmp"
    conda:
        "scvi-env"
    shell:
        r"""
        python bin/003_umap_embedding.py \
            --tissue {wildcards.tissue} \
            --fpath {input[0]} \
            --knee_file {input[1]} \
            --use_matrix {params.use_matrix} \
            --optimum_nn_file {input[2]} \
            --col_by {params.col_by}
        """

# Define clustering resolutions
clustering_resolutions = list(config["clustering_resolutions"])

rule cluster_array:
    input:
        "results/{tissue}/objects/adata_PCAd_batched_umap.h5ad"
    output:
        "results/{tissue}/tables/clustering_array/leiden_{clustering_resolution}/clusters.csv"
    params:
        pref_matrix=config["pref_matrix"]
    resources:
        mem=300000,
        queue='long',
        mem_mb=300000,
        mem_mib=300000,
        disk_mb=300000,
        tmpdir="tmp"
    conda:
        "scvi-env"
    shell:
        r"""
        mkdir -p results/{wildcards.tissue}/tables/clustering_array
        mkdir -p results/{wildcards.tissue}/tables/clustering_array/leiden_{wildcards.clustering_resolution}
        python bin/004_scanpy_cluster.py \
            --tissue {wildcards.tissue} \
            --fpath {input[0]} \
            --pref_matrix {params.pref_matrix} \
            --clustering_resolution {wildcards.clustering_resolution}
        """

rule test_clusters:
    input:
        "results/{tissue}/tables/clustering_array/leiden_{clustering_resolution}/clusters.csv",
        "results/{tissue}/tables/log1p_cp10k_sparse.npz",
        "results/{tissue}/tables/genes.txt",
        "results/{tissue}/tables/cells.txt"
    output:
        "results/{tissue}/tables/clustering_array/leiden_{clustering_resolution}/base-model_report.tsv.gz" 
    resources:
        mem=180000,
        queue='gpu-normal -gpu -',
        mem_mb=180000,
        mem_mib=180000,
        disk_mb=180000,
        tmpdir="tmp"
    singularity:
        "/software/hgi/containers/yascp/yascp.cog.sanger.ac.uk-public-singularity_images-wtsihgi_nf_scrna_qc_6bb6af5-2021-12-23-3270149cf265.sif"
    shell:
        r"""
        python bin/005-scanpy_cluster_validate_resolution-keras.py \
            --tissue {wildcards.tissue} \
            --sparse_matrix_path {input[1]} \
            --clusters_df {input[0]} \
            --genes_f {input[2]} \
            --cells_f {input[3]} \
            --sparsity_l1 0.0001 \
            --number_epoch 25 \
            --batch_size 32 \
            --train_size_fraction 0.67 \
            --output_file results/{wildcards.tissue}/tables/clustering_array/leiden_{wildcards.clustering_resolution}/base
        """


## Define function to aggregate the output of the clustering array results within tissue
def gather_within_tissue(wildcards):
    return expand("results/{tissue}/tables/clustering_array/leiden_{clustering_resolution}/base-model_report.tsv.gz",
                  tissue=wildcards.tissue,
                  clustering_resolution=config["clustering_resolutions"])


# Run the aggregation of the keras tests
rule summarise_cluster_test:
    input:
        "results/{tissue}/objects/adata_PCAd_batched_umap.h5ad",
        gather_within_tissue
    output:
        "results/{tissue}/objects/adata_PCAd_batched_umap_clustered.h5ad",
        "results/{tissue}/tables/optim_resolution.txt",
        "results/{tissue}/tables/markers/markers_all_optim_clusters.txt.gz"
    params:
        MCC_thresh=config["MCC_thresh"],
        pref_matrix=config["pref_matrix"]
    resources:
        mem=180000,
        queue='long',
        mem_mb=180000,
        mem_mib=180000,
        disk_mb=180000,
        tmpdir="tmp"
    conda:
        "scvi-env"
    shell:
        r"""
        mkdir -p results/{wildcards.tissue}/figures/clustering_array_summary
        mkdir -p results/{wildcards.tissue}/tables/markers
        mkdir -p results/{wildcards.tissue}/figures/markers
        python bin/006-summarise_keras.py \
            --tissue {wildcards.tissue} \
            --h5_path {input[0]} \
            --outpath results/{wildcards.tissue}/figures/clustering_array_summary/keras_accuracy \
            --MCC_thresh {params.MCC_thresh} \
            --pref_matrix {params.pref_matrix}
        """

if config["add_celltypist"]:
    rule add_megagut:
        input:
            "results/{tissue}/objects/adata_PCAd_batched_umap_clustered.h5ad"
        output:
            "results/{tissue}/tables/annotation/CellTypist/CellTypist_prob_matrix.csv",
            "results/{tissue}/tables/annotation/CellTypist/CellTypist_decision_matrix.csv",
            "results/{tissue}/tables/annotation/CellTypist/CellTypist_anno_conf.csv"
        params:
            model=config["celltypist_model"],
            model_name=config["celltypist_model_name"],
            pref_matrix=config["pref_matrix"]
        resources:
            mem=180000,
            queue='long',
            mem_mb=180000,
            mem_mib=180000,
            disk_mb=180000,
            tmpdir="tmp"
        conda:
            "scvi-env"
        shell:
            r"""
            mkdir -p results/{wildcards.tissue}/figures/annotation
            mkdir -p results/{wildcards.tissue}/tables/annotation
            mkdir -p results/{wildcards.tissue}/tables/annotation/CellTypist
            python bin/007-CellTypist.py \
                --tissue {wildcards.tissue} \
                --h5_path {input[0]} \
                --pref_matrix {params.pref_matrix} \
                --model {params.model} \
                --model_name {params.model_name}
            """
